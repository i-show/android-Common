/*
 * Copyright (C) 2015  Haiyang Yu Android Source Project
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion COMPILE_SDK_VERSION as int
    buildToolsVersion BUILD_TOOLS_VERSION

    defaultConfig {
        // 注意这里只是设置了appId的前缀 在buildTypes里面有设置后缀
        applicationId APPLICATION_ID
        minSdkVersion MIN_SDK_VERSION as int
        targetSdkVersion TARGET_SDK_VERSION as int
        versionCode generateVersionCode() as int
        versionName VERSION_NAME

        // 增加一个编译版本的时间
        buildConfigField "long", "RELEASE_TIME", System.currentTimeMillis() + "L"

        // 配置 含有的版本类型
        buildConfigField "int", "VERSION_DEV", VERSION_DEV
        buildConfigField "int", "VERSION_SIT", VERSION_SIT
        buildConfigField "int", "VERSION_UAT", VERSION_UAT
        buildConfigField "int", "VERSION_PROD", VERSION_PROD
        // 配置默认版本-为开发版本
        buildConfigField "int", "VERSION_TYPE", VERSION_DEV
        buildConfigField "String", "VERSION_DESCRIPTION", "\"开发版本\""

        // 配置语言信息
        resConfig "zh"
        // NDK 加载lib的文件夹
        ndk {
            abiFilters "armeabi-v7a"
        }
    }

    //签名
    signingConfigs {
        release {
            storeFile file(RELEASE_STOREFILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    productFlavors {
        // TODO
    }

    buildTypes {

        debug {
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
            
            applicationIdSuffix DEV_APPLICATION_ID_SUFFIX
            // AndroidManifest 中配置的文件
            manifestPlaceholders = [
                    // App的名字
                    APP_NAME                : DEV_APP_NAME,
                    // App的ICON
                    APP_ICON                : DEV_APP_ICON,
                    // 百度统计的ID
                    BAIDU_STATISTICS_ID     : DEV_BAIDU_STATISTICS_ID,
                    // 百度统计的渠道描述
                    BAIDU_STATISTICS_CHANNEL: DEV_BAIDU_STATISTICS_CHANNEL
            ]

            // 设置那个版本
            buildConfigField "int", "VERSION_TYPE", VERSION_DEV
            buildConfigField "String", "VERSION_DESCRIPTION", "\"开发版本\""
        }


        sit {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            applicationIdSuffix SIT_APPLICATION_ID_SUFFIX
            // AndroidManifest 中配置的文件
            manifestPlaceholders = [
                    // App的名字
                    APP_NAME                : SIT_APP_NAME,
                    // App的ICON
                    APP_ICON                : SIT_APP_ICON,
                    // 百度统计的ID
                    BAIDU_STATISTICS_ID     : SIT_BAIDU_STATISTICS_ID,
                    // 百度统计的渠道描述
                    BAIDU_STATISTICS_CHANNEL: SIT_BAIDU_STATISTICS_CHANNEL
            ]

            // 设置那个版本
            buildConfigField "int", "VERSION_TYPE", VERSION_SIT
            buildConfigField "String", "VERSION_DESCRIPTION", "\"测试版本\""

            matchingFallbacks = ['debug']
        }


        uat {
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            applicationIdSuffix UAT_APPLICATION_ID_SUFFIX
            // AndroidManifest 中配置的文件
            manifestPlaceholders = [
                    // App的名字
                    APP_NAME                : UAT_APP_NAME,
                    // App的ICON
                    APP_ICON                : UAT_APP_ICON,
                    // 百度统计的ID
                    BAIDU_STATISTICS_ID     : UAT_BAIDU_STATISTICS_ID,
                    // 百度统计的渠道描述
                    BAIDU_STATISTICS_CHANNEL: UAT_BAIDU_STATISTICS_CHANNEL
            ]

            // 设置那个版本
            buildConfigField "int", "VERSION_TYPE", VERSION_UAT
            buildConfigField "String", "VERSION_DESCRIPTION", "\"UAT版本\""

            matchingFallbacks = ['release']
        }

        release {
            debuggable false
            minifyEnabled true
            shrinkResources true

            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release

            applicationIdSuffix PROD_APPLICATION_ID_SUFFIX

            // AndroidManifest 中配置的文件
            manifestPlaceholders = [
                    // App的名字
                    APP_NAME                : PROD_APP_NAME,
                    // App的ICON
                    APP_ICON                : PROD_APP_ICON,
                    // 百度统计的ID
                    BAIDU_STATISTICS_ID     : PROD_BAIDU_STATISTICS_ID,
                    // 百度统计的渠道描述
                    BAIDU_STATISTICS_CHANNEL: PROD_BAIDU_STATISTICS_CHANNEL
            ]

            // 设置那个版本
            buildConfigField "int", "VERSION_TYPE", VERSION_PROD
            buildConfigField "String", "VERSION_DESCRIPTION", "\"生产版本\""
        }
    }


    dexOptions {
        jumboMode true
    }

    sourceSets {
        debug.setRoot('build-types/debug')
        release.setRoot('build-types/release')
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
        // if true, only report errors
        ignoreWarnings true
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }

    dataBinding {
        enabled = true
    }

    // 包名重命名，加上版本及日期
    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            renameAPK(android, variant, output)
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    // 基础库
    implementation project(':common')

    //  百度统计
    implementation 'com.baidu.mobstat:mtj-sdk:3.8.0.6'
    kapt "com.android.databinding:compiler:3.1.4"

    def camerax_version = "1.0.0-beta02"
    implementation "androidx.camera:camera-core:${camerax_version}"
    // If you want to use Camera2 extensions.
    implementation "androidx.camera:camera-camera2:${camerax_version}"

}

